buildscript {
    apply from: 'dependencies.gradle'

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath rootProject.ext.kotlinPlugin
        classpath rootProject.ext.androidPlugin
        classpath rootProject.ext.androidMavenGradlePlugin
    }
}

allprojects {
    repositories {
        jcenter()
        google()
        maven { url 'http://dl.bintray.com/kotlin/kotlinx' }
    }

    group = GROUP
    version = VERSION_NAME
}

apply from: 'dependencies.gradle'

ext.configureLibrary = { proj ->
    delegate = proj

    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'

    android {
        compileSdkVersion proj.compileSdkVersion

        defaultConfig {
            minSdkVersion proj.minSdkVersion
            javaCompileOptions.annotationProcessorOptions.includeCompileClasspath false
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        lintOptions {
            abortOnError getBoolProperty('lintAbortOnError', false)
        }

//        resourcePrefix "${proj.name.replace("-", "_")}_"
    }

    dependencies {
        testImplementation proj.robolectric
        testImplementation proj.junit
        testImplementation proj.truth
        testImplementation proj.mockito
        testImplementation proj.mockitoKotlin
        testImplementation proj.kotlinStdlib
        testImplementation proj.kotlinReflect
        testImplementation project(':power-adapters-test')
        api proj.supportAnnotations
    }

    apply plugin: 'com.github.dcendents.android-maven'
    apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
}

def getBoolProperty(String name, boolean defaultValue) {
    return new Boolean(hasProperty(name) ? project.properties[name].toBoolean() : defaultValue)
}
